apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'at.yawk.dropwizard-nagios'
version = '1.0'

ext {
    dropwizardVersion = "1.2.2"
}

configurations {
    integTest.extendsFrom testRuntime
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    integTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integTest
        runtimeClasspath = output + sourceSets.test.output + compileClasspath
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-metrics:${dropwizardVersion}"
    compile "commons-lang:commons-lang:2.6"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-all:1.9.5"
    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
}

task integTest(type:Test){
    description = "Run integration tests (located in src/integration-test/...)."
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}


if (project.hasProperty('release')) {


        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/yawkat/Nagios-Dropwizard'

                        scm {
                            connection 'scm:git:https://github.com/yawkat/Nagios-Dropwizard.git'
                            developerConnection 'scm:git:git@github.com:yawkat/Nagios-Dropwizard.git'
                            url 'https://github.com/yawkat/Nagios-Dropwizard.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'https://www.apache.org/licenses/LICENSE-2.0'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'yawkat'
                                name = 'Jonas Konrad'
                                email = 'me@yawk.at'
                            }
                        }
                    }
                }
            }
        }

    }
}